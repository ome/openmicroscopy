*** Settings ***

Library  Collections
Library  SwingLibrary


*** Keywords ***

Thumbnail Should Be Blank
        [Documentation]       expects that the thumbnail image is monochromatic
        [Arguments]           ${image name}
        ${is monochromatic}=  Is Thumbnail Monochromatic  ${image name}
        Should Be True        ${is monochromatic}

Thumbnail Should Not Be Blank
        [Documentation]       expects that the thumbnail image is polychromatic
        [Arguments]           ${image name}
        ${is monochromatic}=  Is Thumbnail Monochromatic  ${image name}
        Should Not Be True    ${is monochromatic}

Click Thumbnail
        [Documentation]     click on the thumbnail
        [Arguments]         ${image name}
        ${thumbnail}=       Get Name Of Thumbnail For Image  ${image name}
        Click On Component  ${thumbnail}

Thumbnail Should Not Be Selected
        [Documentation]       expects that the thumbnail is not selected
        [Arguments]           ${image name}
        ${unselected color}=  Get AWT Color               util.ui.border.FrameBorder.DEFAULT_COLOR
        ${thumbnail color}=   Get Thumbnail Border Color  ${image name}
        Should Be Equal       ${unselected color}         ${thumbnail color}

Thumbnail Border Color Should Be
        [Documentation]   check the thumbnail border color
        [Arguments]       ${image name}  ${expected color}
        ${actual color}=  Get Thumbnail Border Color        ${image name}
        Should Be Equal   ${expected color}                 ${actual color}

Thumbnail Images Should Differ
        [Documentation]      check that the thumbnails render different images
        [Arguments]          @{image names}
        @{hashes}=           Create List
        :FOR  ${image name}  IN                  @{image names}
        \  ${hash}=          Get Thumbnail Hash  ${image name}
        \  Append To List    ${hashes}           ${hash}
        List Should Not Contain Duplicates       ${hashes}
