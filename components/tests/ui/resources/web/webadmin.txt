*** Settings ***

Documentation  A resource file containing the application specific keywords
...            that create our own domain specific language. This resource
...            implements keywords for testing HTML version of the test
...            application.
Library        Selenium2Library


*** Variables ***

${DELAY}            0
${BROWSER}          Firefox
${WEBSERVER}        localhost:8080

${WEBADMIN MAIN URL}         http://${WEBSERVER}/webadmin/
${WEBADMIN LOGIN URL}        http://${WEBSERVER}/webclient/login/?url=%2Fwebadmin%2F

${WEBCLIENT MAIN URL}         http://${WEBSERVER}/webclient/
${WEBCLIENT LOGIN URL}        http://${WEBSERVER}/webclient/login/?url=%2Fwebclient%2F

${WEBADMIN URL}     http://${WEBSERVER}/webadmin/experimenters/


${ROOT USERNAME}    root
${ROOT PASSWORD}    omero
${OMERO SERVER}     localhost
${OMERO PORT}       4064

*** Keywords ***

Page Should Be Open
    [Arguments]    ${page url}    ${title}=${None}
    Location Should Be    ${page url}
    Run Keyword If    '${title}'!='None'    Title Should Be    ${title}

Open Browser To Login Page
    [Arguments]    ${main url}    ${login url}
    Open Browser    ${main url}  ${BROWSER}
    Set Selenium Speed    ${DELAY}
    Page Should Be Open    ${login url}    OMERO.web - Login

Open Browser To Webadmin Login Page
    Open Browser To Login Page    ${WEBADMIN MAIN URL}    ${WEBADMIN LOGIN URL}

Open Browser To Webclient Login Page
    Open Browser To Login Page    ${WEBCLIENT MAIN URL}    ${WEBCLIENT LOGIN URL}

Log In As
    [Arguments]    ${username}    ${password}    ${server}    ${port}    ${ssl}=${False}
    Select From List By Label    id_server    ${server}:${port}
    Run Keyword If    ${ssl}    Select Checkbox    id_ssl
    Input Text  username  ${username}
    Input Text  password  ${password}
    Click Button    Login

Log In As Root
    Log In As    ${ROOT USERNAME}    ${ROOT PASSWORD}    ${OMERO SERVER}    ${OMERO PORT}

Log In Secure As Root
    Log In As    ${ROOT USERNAME}    ${ROOT PASSWORD}    ${OMERO SERVER}    ${OMERO PORT}    ${True}

Log Out
    Click Element    xpath=//div[@id='show_user_dropdown']/span
    Click Link    link=Logout
    Page Should Be Open    ${WEBCLIENT LOGIN URL}    OMERO.web - Login

Unique name
    [Arguments]    ${name}
    ${unique}    Get Time    epoch
    [Return]    ${name}_${unique}
