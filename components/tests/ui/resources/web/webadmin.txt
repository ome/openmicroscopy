*** Settings ***

Documentation  A resource file containing the application specific keywords
...            that create our own domain specific language. This resource
...            implements keywords for testing HTML version of the test
...            application.
Library        Selenium2Library


*** Keywords ***

Page Should Be Open
    [Arguments]    ${page url}    ${title}=${None}
    Location Should Be    ${page url}
    Run Keyword If    '${title}'!='None'    Title Should Be    ${title}


Open Browser To Login Page
    [Arguments]    ${main url}    ${login url}
    Open Browser    ${main url}  ${BROWSER}
    Set Selenium Speed    ${DELAY}
    Page Should Be Open    ${login url}    OMERO.web - Login

Open Browser To Webadmin Login Page
    Open Browser To Login Page    ${WEBADMIN WELCOME URL}    ${WEBADMIN LOGIN URL}

Open Browser To Webclient Login Page
    Open Browser To Login Page    ${WELCOME URL}    ${LOGIN URL}


Log In As
    [Arguments]    ${username}    ${password}    ${server}    ${port}    ${ssl}=${False}
    Select From List By Label    id_server    ${server}:${port}
    Run Keyword If    ${ssl}    Select Checkbox    id_ssl
    Input Text  username  ${username}
    Input Text  password  ${password}
    Click Button    Login

Log In As Root
    Log In As    ${ROOT USERNAME}    ${ROOT PASSWORD}    ${OMERO HOST}    ${OMERO PORT}

Log In Secure As Root
    Log In As    ${ROOT USERNAME}    ${ROOT PASSWORD}    ${OMERO HOST}    ${OMERO PORT}    ${True}

Log Out
    Click Element    xpath=//div[@id='show_user_dropdown']/span
    Click Link    link=Logout
    Page Should Be Open    ${LOGIN URL}?url=%2Fwebclient%2F    OMERO.web - Login

Unique name
    [Arguments]    ${name}
    ${unique}    Get Time    epoch
    [Return]    ${name}_${unique}


Page Should Contain Input Field 
    [Arguments]    ${label}    ${value}
    Page Should Contain    ${label}
    Page Should Contain Textfield    name=${value}

Page Should Contain Choice Field
    [Arguments]    ${label}    ${name}
    Page Should Contain    ${label}
    Page Should Contain Textfield    ${name}

Page Should Contain Password Field
    [Arguments]    ${label}    ${name}
    Page Should Contain    ${label}
    Page Should Contain Element    name=${name}

Page Should Contain Checkbox Field
    [Arguments]    ${label}    ${name}    ${selected}=${False}
    Page Should Contain    ${label}
    Page Should Contain Checkbox    ${name}
    Run Keyword If    ${selected}    Checkbox Should Be Selected    ${name}    ELSE    Checkbox Should Not Be Selected    ${name}

Page Should Contain Radio Field
    [Arguments]    ${label}    ${name}    ${value}
    Page Should Contain    ${label}
    Page Should Contain Radio Button    ${name}
    Radio Button Should Be Set To    ${name}    ${value}

