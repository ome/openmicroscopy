*** Settings ***
Documentation     Tests delete of Projects, Datasets, Images

Resource          ../../resources/config.txt
Resource          ../../resources/web/login.txt
Resource          ../../resources/web/tree.txt

Suite Setup         Run Keywords  User "${USERNAME}" logs in with password "${PASSWORD}"  Maximize Browser Window
Suite Teardown      Close all browsers


*** Variables ***

${thumbnailsXpath}      //ul[@id='dataIcons']//div[contains(@class, 'image')]

*** Keywords ***

Clear activity
    [Documentation]         Clear any activities from earlier tests etc.
    Click Element           id=launch_activities
    Click Element           id=clear_activities

*** Test Cases ***

Test Delete Project
    [Documentation]     Create and Delete a Project

    Clear activity
    Select Experimenter
    ${pid}=                                 Create project      robot test delete
    Click Element                           refreshButton
    Wait Until Page Contains Element        id=project-${pid}
    Click Element                           id=deleteButton
    Wait Until Element Is Visible           id=delete-dialog-form
    Click Dialog Button                     Yes
    # Wait for activities to show job done, then refresh tree...
    Wait Until Keyword Succeeds     ${TIMEOUT}   ${INTERVAL}   Page Should Contain Element     xpath=//span[@id='jobstatus'][contains(text(),'1')]
    Click Element                           refreshButton
    # On POST success, Experimenter should be selected and project removed from tree
    Wait Until Page Contains Element        xpath=//li[@rel='experimenter']/a[contains(@class, 'jstree-clicked')]
    Page Should Not Contain Element         id=project-${pid}


Test Delete Project Dataset
    [Documentation]     Create and Delete a Project containing a Dataset

    Clear activity
    Select Experimenter
    ${pid}=                                 Create project      robot test delete
    ${did}=                                 Create Dataset      robot test deleteChildren
    Click Element                           refreshButton
    Wait Until Page Contains Element        id=project-${pid}
    Click Element                           css=#project-${pid}>a
    Wait Until Page Contains Element        xpath=//tr[contains(@class,'data_heading_id')]/td/strong[contains(text(), '${pid}')]  ${WAIT}
    Click Element                           id=deleteButton
    Wait Until Element Is Visible           id=delete-dialog-form
    Click Dialog Button                     Yes
    # Wait for activities to show job done, then refresh tree...
    Wait Until Keyword Succeeds     ${TIMEOUT}   ${INTERVAL}   Page Should Contain Element     xpath=//span[@id='jobstatus'][contains(text(),'1')]
    Click Element                           refreshButton
    Wait Until Page Contains Element        xpath=//li[@rel='experimenter']/a[contains(@class, 'jstree-clicked')]
    Page Should Not Contain Element         id=project-${pid}
    # Dataset should be Deleted too
    Page Should Not Contain Element         id=dataset-${did}


Test Delete Images in Dataset
    [Documentation]     Deletes images pre-imported into a dataset named "Delete"

    Clear activity
    Select Experimenter
    # Click on Dataset, wait for thumbnails and count them
    Click Element                           xpath=//div[@id='dataTree']//li[contains(@rel, 'dataset')]/a[contains(text(), 'Delete')]
    Wait Until Page Contains Element        id=dataIcons
    ${thumbCount}=                          Get Matching Xpath Count     ${thumbnailsXpath}
    # Click first image in Tree
    Click Element                           xpath=//div[@id='dataTree']//li[contains(@rel, 'image')]/a
    Click Element                           id=deleteButton
    Wait Until Element Is Visible           id=delete-dialog-form
    Click Dialog Button                     Yes
    # Should see almost instant removal of 1 thumbnail...
    ${delThumbCount}=                       Evaluate   ${thumbCount} - 1
    Wait Until Keyword Succeeds     ${TIMEOUT}      ${INTERVAL}   Xpath Should Match X Times   ${thumbnailsXpath}   ${delThumbCount}
    # Wait for activities to show job done, then refresh tree...
    Wait Until Keyword Succeeds     ${TIMEOUT}   ${INTERVAL}   Page Should Contain Element     xpath=//span[@id='jobstatus'][contains(text(),'1')]
    Click Element                           refreshButton
    Wait Until Page Contains Element        xpath=//li[@rel='experimenter']//a[contains(@class, 'jstree-clicked')]  ${WAIT}
    Wait Until Page Contains Element        id=dataIcons    ${WAIT}
    Xpath Should Match X Times   ${thumbnailsXpath}   ${delThumbCount}


Test Delete Images in Share
    [Documentation]     Deletes pre-imported images after putting them in Share

    Clear activity
    Select Experimenter
    # Add image in 'Delete' Dataset to Share
    Click Element                               xpath=//div[@id='dataTree']//li[contains(@rel, 'dataset')]/a[contains(text(), 'Delete')]
    Wait Until Page Contains Element            id=dataIcons
    # Click first image in Tree
    Click Element                               xpath=//div[@id='dataTree']//li[contains(@rel, 'image')]/a
    Wait Until Page Contains Element            xpath=//div[@id='dataTree']//li[contains(@rel, 'image')]/a[contains(@class, 'jstree-clicked')]
    ${imageId}=                                 Get Element Attribute    xpath=//div[@id='dataTree']//li[contains(@rel, 'image')]@id

    Click Element                               createshareButton
    Wait Until Page Contains Element            id_message
    Input Text                                  id_message      Robot Test Delete Images in Share
    Click Element                               xpath=//div[@id='id_members_chosen']/ul[@class='chosen-choices']
    Page Should Contain Element                 xpath=//div[@id='id_members_chosen']/div[@class='chosen-drop']/ul[@class='chosen-results']
    Click Element                               xpath=//div[@id='id_members_chosen']/div[@class='chosen-drop']/ul[@class='chosen-results']/li[@data-option-array-index='1']

    Submit Form                                 create_share_form
    Wait Until Page Contains Element            id=share_dialog_form
    Page Should Contain Element                 id=deleteOthersWarning
    Wait Until Element Is Visible               xpath=//button[contains(@class,'ui-button')]/span[contains(text(), 'OK')]
    Click Dialog Button                         OK

    Go To                                       ${WELCOME URL}public/
    Wait Until Page Contains Element            id=Public
    # Wait Until Page Contains Element          xpath=//div[@id='dataTree']//li[@rel='share']/a
    # Click Element                             xpath=//div[@id='dataTree']//li[@rel='share']/a
    Go To                                       ${WELCOME URL}?show=${imageId}
    Wait Until Page Contains Element            id=dataIcons
    Click Element                               id=deleteButton
    Wait Until Element Is Visible               id=delete-dialog-form
    Click Dialog Button                         Yes
    Wait Until Keyword Succeeds     ${TIMEOUT}   ${INTERVAL}   Page Should Contain Element     xpath=//span[@id='jobstatus'][contains(text(),'1')]
    Go To                                       ${WELCOME URL}public
    Wait Until Page Contains Element            xpath=//div[@id='dataTree']//li[@rel='share']/a
    Click Element                               xpath=//div[@id='dataTree']//li[@rel='share']/a
    # Should be image with id='image-123', with 'Object Deleted' in Tree and icons
    Wait Until Page Contains Element            xpath=//div[@id='dataTree']//li[@id='${imageId}'][contains(text(), 'Object deleted')]
    Wait Until Page Contains Element            xpath=//ul[@id='dataIcons']//li[@title='Object deleted']

