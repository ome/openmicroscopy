*** Settings ***
Documentation     Tests creation of Projects and Datasets
...               and enabled status of create P/D/S options
...
...               This test requires P/D/I in hierarchy.

Resource          ../../resources/config.txt
Resource          ../../resources/web/login.txt
Resource          ../../resources/web/tree.txt
Resource          ../../resources/web/thumbs.txt

*** Test Cases ***

Test Container Creation Enabled
    [Documentation]     Select User, Project, Dataset, Image and checks
    ...                 whether the toolbar/right-click menu options for
    ...                 creating various containers are enabled.

    User "${USERNAME}" logs in with password "${PASSWORD}"

    Tree Should Be Visible
    Wait For Node To Be Visible                 ${FULL NAME}
    Node Should Be Visible                      Orphaned images
    Create Button Should Be Enabled             project
    Create Button Should Be Enabled             dataset
    Create Button Should Be Enabled             screen
    Node Popup Menu Item Should Be Enabled      Project    ${FULL NAME}
    Node Popup Menu Item Should Be Enabled      Dataset    ${FULL NAME}
    Node Popup Menu Item Should Be Enabled      Screen     ${FULL NAME}
    Node Popup Menu Item Should Be Disabled     Delete     ${FULL NAME}
    Select And Expand Project
    Create Button Should Be Enabled             project
    Create Button Should Be Enabled             dataset
    Create Button Should Be Enabled             screen
    Node Popup Menu Item Should Be Disabled     Project
    Node Popup Menu Item Should Be Enabled      Dataset
    Node Popup Menu Item Should Be Disabled     Screen
    Node Popup Menu Item Should Be Enabled      Delete
    Select And Expand Dataset
    Create Button Should Be Enabled             project
    Create Button Should Be Enabled             dataset
    Create Button Should Be Enabled             screen
    Node Popup Menu Item Should Be Disabled     Project
    Node Popup Menu Item Should Be Disabled     Dataset
    Node Popup Menu Item Should Be Disabled     Screen
    Node Popup Menu Item Should Be Enabled      Delete
    ${imageId}=                                 Select And Expand Image
    Create Button Should Be Enabled             project
    Create Button Should Be Enabled             dataset
    Create Button Should Be Enabled             screen
    Node Popup Menu Item Should Be Enabled      Project
    Node Popup Menu Item Should Be Enabled      Dataset
    Node Popup Menu Item Should Be Enabled      Screen
    Node Popup Menu Item Should Be Enabled      Delete

    Thumbnail Should Be Selected                ${imageId}
    Key Down                                    40    # '40' is 'down' key    # Press Key    dataTree    /40
    Thumbnail Should Not Be Selected            ${imageId}
    Node Should Not Be Selected                 ${imageId}
    Click Thumbnail                             ${imageId}
    Thumbnail Should Be Selected                ${imageId}
    Node Should Be Selected                     ${imageId}

Test Create Project Dataset
    [Documentation]     Test Creation of Project & Dataset, wait for right panel to load
    ...                 and check that the new container is selected in the tree
    Select Experimenter
    Create Project
    Create Dataset

    [Teardown]    Close Browser
