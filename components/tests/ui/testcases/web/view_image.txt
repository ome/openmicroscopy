*** Settings ***
Documentation     Tests browsing to an Image and opening Image Viewer

Resource          ../../resources/config.txt
Resource          ../../resources/web/login.txt
Resource          ../../resources/web/tree.txt

Library           Collections

Suite Setup         Run Keywords  User "${USERNAME}" logs in with password "${PASSWORD}"  Maximize Browser Window
Suite Teardown      Close all browsers

*** Keywords ***

Check Image Viewer
    [Arguments]                         ${title}
    # Wait untill popup windows is fully initialiazed
    # Image Viewer title is the image name.
    Wait Until Keyword Succeeds  ${TIMEOUT}     ${INTERVAL}     Select Window   title=${title}
    # Loading of image within viewport indicates all is OK
    Wait Until Page Contains Element    xpath=//img[@id='weblitz-viewport-img']     ${WAIT}
    ${titles}=                          Get Window Titles
    List Should Contain Value           ${titles}               ${title}
    # Should have 2 windows open
    ${windowIds}=                       Get Window Identifiers
    Length Should Be                    ${windowIds}            2
    # Close Popup window
    Close Window
    # Select parent window
    Select Window

*** Test Cases ***

Test Open Viewer
    [Documentation]     Tests double-click to open image viewer

    Tree Should Be Visible
    ${imageId}=                         Select And Expand Image
    ${nodeId}=                          Wait For Image Node         ${imageId}
    ${imageName}=                       Wait For General Panel And Return Name      Image
    # Open Image Viewer 3 different ways and check
    Click Element                       xpath=//a[@title='Open full image viewer in new window']
    Check Image Viewer                  ${imageName}
    Double Click Element                xpath=//li[@id='image_icon-${imageId}']//img
    Check Image Viewer                  ${imageName}
    Double Click Element                css=#${nodeId}>a
    Check Image Viewer                  ${imageName}


Test Prev Next Buttons

    ${imageId}=                         Select And Expand Image
    ${nodeId}=                          Wait For Image Node         ${imageId}
    ${imageName}=                       Wait For General Panel And Return Name      Image
    # Check we have a 'Next' image, but no 'Previous' image
    Page Should Contain Element         xpath=//ul[@id='dataIcons']/li[contains(@class, 'ui-selected')]/following-sibling::li[contains(@class, 'row')]
    Page Should Not Contain Element     xpath=//ul[@id='dataIcons']/li[contains(@class, 'ui-selected')]/preceding-sibling::li[contains(@class, 'row')]

    # Open image viewer
    Double Click Element                css=#${nodeId}>a
    Wait Until Keyword Succeeds  ${TIMEOUT}     ${INTERVAL}     Select Window   title=${imageName}

    # Prev button should be disabled, Next button enabled
    Element Should Be Disabled          id=prevImage
    Element Should Be Enabled           id=nextImage

    # Clicking Next button will enable Prev button
    Click Element                       id=nextImage
    Wait Until Page Contains Element    xpath=//button[@id='prevImage' and not (@disabled='disabled')]
