*** Settings ***

Library   SwingLibrary
Library   org.openmicroscopy.shoola.keywords.IconCheckLibrary
Library   org.openmicroscopy.shoola.keywords.JTreeLibrary
Library   org.openmicroscopy.shoola.keywords.StaticFieldLibrary
Library   org.openmicroscopy.shoola.keywords.ThumbnailCheckLibrary
Resource  ../../../resources/config.txt
Resource  ../../../resources/insight/login.txt
Resource  ../../../resources/insight/state.txt
Resource  ../../../resources/insight/thumbnail.txt


*** Variables ***

@{normal images}  test.jpg  tinyTest.d3d.dv
@{blank images}   red.png   green.png  blue.png

*** Test Cases ***

Blank Thumbnails
        [Documentation]      check that only the expected thumbnails are blank
        Select Window        tree viewer window
        Expand Browser       project
        ${image icon name}=  Get Insight Image Icon Name  treeviewer          IMAGE
        :FOR  ${image name}  IN  @{normal images}
        \  Get Tree Path With Icon And Name               ${image icon name}  ${image name}  project tree
        \  Thumbnail Should Not Be Blank                  ${image name}
        :FOR  ${image name}  IN  @{blank images}
        \  Get Tree Path With Icon And Name               ${image icon name}  ${image name}  project tree
        \  Thumbnail Should Be Blank                      ${image name}

Rendered Thumbnails
        [Documentation]      check that the proper image thumbnails actually differ
        Select Window        tree viewer window
        Expand Browser       project
        ${image icon name}=  Get Insight Image Icon Name  treeviewer          IMAGE
        :FOR  ${image name}  IN  @{normal images}
        \  Get Tree Path With Icon And Name               ${image icon name}  ${image name}  project tree
        Thumbnail Images Should Differ                    @{normal images}
