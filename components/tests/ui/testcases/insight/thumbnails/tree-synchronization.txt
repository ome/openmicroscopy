*** Settings ***

Library   SwingLibrary
Library   org.openmicroscopy.shoola.keywords.IconCheckLibrary
Library   org.openmicroscopy.shoola.keywords.JTreeLibrary
Library   org.openmicroscopy.shoola.keywords.StaticFieldLibrary
Library   org.openmicroscopy.shoola.keywords.ThumbnailCheckLibrary
Resource  ../../../resources/config.txt
Resource  ../../../resources/insight/login.txt
Resource  ../../../resources/insight/state.txt
Resource  ../../../resources/insight/thumbnail.txt


*** Variables ***

${first image}   test.bmp
${second image}  test.jpg


*** Test Cases ***

Tree Thumbnail Synchronization
        [Documentation]                   check that selection is synchronized between project tree and data browser
        Select Window                     tree viewer window
        Expand Browser                    project
        ${image icon name}=               Get Insight Image Icon Name       treeviewer                            IMAGE
        ${first tree path}=               Get Tree Path With Icon And Name  ${image icon name}  ${first image}   project tree
        ${second tree path}=              Get Tree Path With Icon And Name  ${image icon name}  ${second image}  project tree
        Click On Tree Node                project tree                      ${first tree path}
        ${selected border color}=         Get Thumbnail Border Color        ${first image}
        ${unselected border color}=       Get AWT Color                     util.ui.border.FrameBorder.DEFAULT_COLOR
        Should Not Be Equal               ${selected border color}          ${unselected border color}
        Thumbnail Should Not Be Selected  ${second image}
        Tree Node Should Be Selected      project tree                      ${first tree path}
        Tree Node Should Not Be Selected  project tree                      ${second tree path}
        Click On Tree Node                project tree                      ${second tree path}
        Thumbnail Should Not Be Selected  ${first image}
        Thumbnail Border Color Should Be  ${second image}                   ${selected border color}
        Tree Node Should Not Be Selected  project tree                      ${first tree path}
        Tree Node Should Be Selected      project tree                      ${second tree path}
        Click Thumbnail                   ${first image}
        Thumbnail Border Color Should Be  ${first image}                    ${selected border color}
        Thumbnail Should Not Be Selected  ${second image}
        Tree Node Should Be Selected      project tree                      ${first tree path}
        Tree Node Should Not Be Selected  project tree                      ${second tree path}
        Click Thumbnail                   ${second image}
        Thumbnail Should Not Be Selected  ${first image}
        Thumbnail Border Color Should Be  ${second image}                   ${selected border color}
        Tree Node Should Not Be Selected  project tree                      ${first tree path}
        Tree Node Should Be Selected      project tree                      ${second tree path}
