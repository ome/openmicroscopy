*** Settings ***

Library   SwingLibrary
Library   org.openmicroscopy.shoola.keywords.IconCheckLibrary
Library   org.openmicroscopy.shoola.keywords.JTreeLibrary
Resource  ../../../resources/config.txt
Resource  ../../../resources/insight/login.txt
Resource  ../../../resources/insight/state.txt


*** Test Cases ***

Icon Images
        [Documentation]                check that GUI elements have the correct icon image
        Select Window                  tree viewer window
        Expand Browser                 project
        ${icon name}=                  Get Image Icon Name            new container button
        Should Be Equal                ${icon name}                   nuvola_filenew16.png
        Tree Node Should Be Visible    project tree                   ${FULL NAME}
        Expand Tree Node               project tree                   ${FULL NAME}
        Tree Node Should Be Visible    project tree                   ${FULL NAME}|Orphaned Images
        ${orphaned images icon name}=  Get Tree Node Image Icon Name  ${FULL NAME}|Orphaned Images  project tree
        Should Be Equal                ${orphaned images icon name}   nuvola_trashcan_empty16.png
        Return To Login
        Log In As                      user=${ROOT USERNAME}          pass=${ROOT PASSWORD}
        ${icon name}=                  Get Image Icon Name            administration browser
        Should Be Equal                ${icon name}                   nuvola_personal16.png
