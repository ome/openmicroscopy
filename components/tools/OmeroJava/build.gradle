plugins {
    id "java"
    id "java-library"
    id "org.openmicroscopy.project" version "5.5.1"
}

group = "org.openmicroscopy"
version = "5.6.0"

ext {
    // Load omero.properties
    Properties omeroProps = loadOmeroProperties()
    omeroCommonTestVersion = omeroProps.getProperty("versions.omero-common-test")
    omeroBlitzVersion = omeroProps.getProperty("versions.omero-blitz")
    omeroGatewayVersion = omeroProps.getProperty("versions.omero-gateway")
}

repositories {
    jcenter()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    test {
        java {
            srcDirs = ['test']
        }
    }
}

dependencies {
    api("org.springframework:spring-context:4.3.14.RELEASE")
    implementation("com.zeroc:icegrid:3.6.5")
    testImplementation("junit:junit:4.12")
    testImplementation("org.testng:testng:6.14.2")
    implementation('commons-collections:commons-collections:3.2.2')
    implementation('commons-beanutils:commons-beanutils:1.9.3')
    implementation("org.openmicroscopy:omero-common-test:$omeroCommonTestVersion")
    implementation("org.openmicroscopy:omero-gateway:$omeroGatewayVersion")
}

test {

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    // enable TestNG support (default is JUnit)
    useTestNG() {
        // Run the unit test only
        if (project.hasProperty('test-unit')) {
            suites './test/unit.testng.xml'
        } else {
            suites './test/unit.testng.xml'
            suites './test/gateway.testng.xml'
            suites './test/fs.testng.xml'
            suites './test/integration.testng.xml'
        }
    }
    dependsOn cleanTest 
}

Properties loadOmeroProperties() {
    Properties props = new Properties()
    file("../../../etc/omero.properties").withInputStream { props.load(it) }
    props
}
